<!DOCTYPE html>
<html>
	<head>
		<title>Page Title</title>
		<script id="vertexShader" type="x-shader/x-vertex">#version 300 es
in vec2 coordinates;

out vec2 texCoords;

void main() {
	gl_Position = vec4(coordinates, 0.0, 1.0);
	texCoords = coordinates * 0.5 + 0.5;
}
		</script>
		<script id="initFragShader" type="x-shader/x-fragment">#version 300 es
precision highp float;

in highp vec2 texCoords;

uniform float texSize;
uniform sampler2D sampler;

out vec4 FragColor;

vec2 getPixelCoords(vec2 texCoords) {
	return texCoords * texSize - 0.5;
}

vec2 getTexCoords(vec2 pixelCoords) {
	return (pixelCoords + 0.5) / texSize;
}

vec4 getPixel(vec2 pixelCoords) {
	return texture(sampler, getTexCoords(pixelCoords));
}

float hypot(vec2 v) {
	return sqrt(v.x * v.x + v.y * v.y);
}

float sum(vec2 v) {
	return v.x + v.y;
}

float square(float x) {
	return x * x;
}

float equilibriumVelocityDistribution(vec2 averageVelocity, vec2 velocity) {
	return
		  1.0
		+ 3.0 * sum(velocity * averageVelocity)
		+ 9.0 / 2.0 * square(sum(velocity * averageVelocity))
		- 3.0 / 2.0 * sum(averageVelocity * averageVelocity);
}

void main() {
	vec4 color = vec4(0.0, 0.0, 0.0, 0.0);
	
	ivec2 pixelCoords = ivec2(getPixelCoords(texCoords) + 0.5);
	ivec2 centerPixelCoords = pixelCoords - pixelCoords % 3 + 1;
	
	int direction = pixelCoords.x % 3 + (pixelCoords.y % 3) * 3;
	
	vec2[9] velocity = vec2[](
		vec2(-1.0, -1.0),
		vec2( 0.0, -1.0),
		vec2( 1.0, -1.0),
		vec2(-1.0,  0.0),
		vec2( 0.0,  0.0),
		vec2( 1.0,  0.0),
		vec2(-1.0,  1.0),
		vec2( 0.0,  1.0),
		vec2( 1.0,  1.0)
	);
	
	float[9] weights = float[](
		1.0 / 36.0,
		1.0 /  9.0,
		1.0 / 36.0,
		1.0 /  9.0,
		4.0 /  9.0,
		1.0 /  9.0,
		1.0 / 36.0,
		1.0 /  9.0,
		1.0 / 36.0
	);

	float speed = 0.03;
	
	if (
		centerPixelCoords.y == 1 ||
		centerPixelCoords.y == int(texSize) - 2 ||
		centerPixelCoords.x == 1 ||
		centerPixelCoords.x == int(texSize) - 2 ||
		hypot(vec2(centerPixelCoords - ivec2(int(texSize * 0.3), int(texSize * 0.5)))) < 100.0
	) {
		color = vec4(0.0, 1.0, 0.0, 0.0);
	} else if (centerPixelCoords.x == 4) {
		color = vec4((max(weights[direction], 1.0/9.0) - 1.0/9.0) * 3.0 * 1.0, 2.0, (1.0 - square(2.0 * texCoords.y - 1.0)) * speed, 0.0);
	} else if (centerPixelCoords.x == int(texSize) - 5) {
		color = vec4((max(weights[direction], 1.0/9.0) - 1.0/9.0) * 3.0 * (1.0 - (1.0 - square(2.0 * texCoords.y - 1.0)) * 3.0 * speed), 3.0, 0.0, 0.0);
	} else {
		color = vec4(weights[direction] * 1.0 * equilibriumVelocityDistribution(vec2((1.0 - square(2.0 * texCoords.y - 1.0)) * 2.0 * speed, 0.0), velocity[direction]), 0.0, 0.0, 0.0);
	}
	
	FragColor = color;
}
		</script>
		<script id="compFragShader" type="x-shader/x-fragment">#version 300 es
precision highp float;

in highp vec2 texCoords;

uniform float texSize;
uniform sampler2D sampler;

out vec4 FragColor;

vec4 getPixel(vec2 pixelCoords) {
	/*if (pixelCoords.x < 0.0 || pixelCoords.y < 0.0 || pixelCoords.x > texSize - 1.0 || pixelCoords.y > texSize - 1.0) {
		return vec4(0.0, 1.0, 0.0, 0.0);
	}*/
	
	return texture(sampler, (pixelCoords + 0.5) / texSize);
}

vec2 getPixelCoords(vec2 texCoords) {
	return texCoords * texSize - 0.5;
}

float sum(vec2 v) {
	return v.x + v.y;
}

float square(float x) {
	return x * x;
}

float equilibriumVelocityDistribution(vec2 averageVelocity, vec2 velocity) {
	return
		  1.0
		+ 3.0 * sum(velocity * averageVelocity)
		+ 9.0 / 2.0 * square(sum(velocity * averageVelocity))
		- 3.0 / 2.0 * sum(averageVelocity * averageVelocity);
}

void main() {
	float tau = 1.985;
	
	vec2 pixelCoords = getPixelCoords(texCoords);
	vec4 pixel = getPixel(pixelCoords);

	vec2 centerPixelCoords = pixelCoords - vec2(ivec2(pixelCoords + 0.5) % 3 - 1);
	vec4 centerPixel = getPixel(centerPixelCoords);
	
	vec4 color;
	
	if (centerPixel.g == 0.0) {
		vec2[9] velocity = vec2[](
			vec2(-1.0, -1.0),
			vec2( 0.0, -1.0),
			vec2( 1.0, -1.0),
			vec2(-1.0,  0.0),
			vec2( 0.0,  0.0),
			vec2( 1.0,  0.0),
			vec2(-1.0,  1.0),
			vec2( 0.0,  1.0),
			vec2( 1.0,  1.0)
		);
		
		float[9] weights = float[](
			1.0 / 36.0,
			1.0 /  9.0,
			1.0 / 36.0,
			1.0 /  9.0,
			4.0 /  9.0,
			1.0 /  9.0,
			1.0 / 36.0,
			1.0 /  9.0,
			1.0 / 36.0
		);
		
		float[9] particleQuantity;
		
		float currentTotalParticles = 0.0;
		
		for (int i = 0; i < 9; i++) {
			currentTotalParticles += getPixel(centerPixelCoords + velocity[i]).r;
		}
		
		vec2 currentAverageVelocity = vec2(0.0);
		
		if (currentTotalParticles != 0.0) {
			for (int i = 0; i < 9; i++) {
				currentAverageVelocity += getPixel(centerPixelCoords + velocity[i]).r / currentTotalParticles * velocity[i];
			}
		}
		
		for (int i = 0; i < 9; i++) {
			vec4 neighbor = getPixel(centerPixelCoords + velocity[i] * -3.0);
			
			if (neighbor.g == 1.0) {
				particleQuantity[i] = getPixel(centerPixelCoords + velocity[i] * -1.0).r;
			} else if (neighbor.g == 2.0) {
				particleQuantity[i] = weights[i] * neighbor.r * equilibriumVelocityDistribution(currentAverageVelocity * 0.5 + neighbor.ba, velocity[i]);
			} else if (neighbor.g == 3.0) {
				particleQuantity[i] = weights[i] * neighbor.r * equilibriumVelocityDistribution(currentAverageVelocity * 0.5, velocity[i]);
			} else {
				particleQuantity[i] = getPixel(centerPixelCoords + velocity[i] * -2.0).r;
			}
			
			if (centerPixel.g == 2.0) {
				particleQuantity[i] += centerPixel.b * weights[i];
			}
		}
		
		float totalParticles = 0.0;
		
		for (int i = 0; i < 9; i++) {
			totalParticles += particleQuantity[i];
		}
		
		vec2 averageVelocity = vec2(0.0);
		
		if (totalParticles != 0.0) {
			for (int i = 0; i < 9; i++) {
				averageVelocity += particleQuantity[i] / totalParticles * velocity[i];
			}
		}
		
		float[9] equilibriumParticleQuantity;
		
		for (int i = 0; i < 9; i++) {
			equilibriumParticleQuantity[i] =
				  weights[i]
				* totalParticles
				* equilibriumVelocityDistribution(averageVelocity, velocity[i]);
		}
		
		for (int i = 0; i < 9; i++) {
			particleQuantity[i] = mix(particleQuantity[i], clamp(equilibriumParticleQuantity[i], 0.0, 10.0), tau);
		}
		
		color = vec4(particleQuantity[int(pixelCoords.x + 0.5) % 3 + 3 * (int(pixelCoords.y + 0.5) % 3)], pixel.gba);
	} else {
		color = pixel;
	}
	
	FragColor = color;
}
		</script>
		<script id="displayFragShader" type="x-shader/x-fragment">#version 300 es
precision highp float;

in highp vec2 texCoords;

uniform float texSize;
uniform sampler2D sampler;
uniform int speed_vorticity; // 0 -> show speed, 1 -> show vorticity

out vec4 FragColor;

vec2 getVelocity(vec2 texCoords) {
	vec2 velocity = vec2(0.0);
	
	float totalParticles = 0.0;
	vec2 momentum = vec2(0.0);
	
	for (int x = -1; x < 2; x++) {
		for (int y = -1; y < 2; y++) {
			float particles = texture(sampler, texCoords + vec2(x, y) / texSize).r;
			totalParticles += particles;
			momentum += particles * vec2(x, y);
		}
	}
	
	if (totalParticles != 0.0) {
		velocity = momentum / totalParticles;
	}
	
	return velocity;
}

void main() {
	float totalParticles = 0.0;
	
	vec4 color;
	
	if (texture(sampler, texCoords).g == 1.0) {
		color = vec4(0.0, 0.0, 0.0, 1.0);
	} else {
		vec2 momentum = vec2(0.0);
		
		for (int x = -1; x < 2; x++) {
			for (int y = -1; y < 2; y++) {
				float particles = texture(sampler, texCoords + vec2(x, y) / texSize).r;
				totalParticles += particles;
				momentum += particles * vec2(x, y);
			}
		}
		
		vec2 velocity = vec2(0.0);
		
		if (totalParticles != 0.0) {
			velocity = momentum / totalParticles;
		}
		
		float vorticity = 0.0;
		
		bool skipVorticity = false;
		
		for (int x = -1; x < 2; x++) {
			for (int y = -1; y < 2; y++) {
				skipVorticity = skipVorticity || texture(sampler, texCoords - 3.0 * vec2(x, y) / texSize).g > 1.0;
				vorticity += dot(vec2(-y, x), getVelocity(texCoords - 3.0 * vec2(x, y) / texSize));
			}
		}
		
		if (skipVorticity) {
			vorticity = 0.0;
		}
		
		totalParticles = vorticity;
		
		float speed = sqrt(velocity.x * velocity.x + velocity.y * velocity.y);
		
		float zeroPoint = 0.0;
		float pressureBrightness = 5.0 * float(speed_vorticity); // not pressure, vorticity, please forgive my failure to rename variables
		float velocityBrightness = 2.0 * (1.0 - float(speed_vorticity));
		
		vec3 pressureColor = vec3(max(totalParticles - zeroPoint, 0.0), 0.0, max(zeroPoint - totalParticles, 0.0));
		vec3 velocityColor = vec3(vec3(sqrt(speed)));
		color = vec4(pressureColor * pressureBrightness + velocityColor * velocityBrightness, 1.0);
	}
	
	FragColor = color;
}
		</script>
	</head>
	<body>
		<div id="container">
			<span id="speedMeter"></span>
			<br />
			<span id="fpsCounter"></span>
			<br />
			<span id="throughputMeter"></span>
		</div>
		<canvas id="can"></canvas>
	</body>
</html>

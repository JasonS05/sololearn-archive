<!-- WebGL1 version here: https://code.sololearn.com/W805Ghe3wUMF -->

<!DOCTYPE html>
<html>
	<head>
		<title>Page Title</title>
		<script id="vertexShader" type="x-shader/x-vertex">#version 300 es
in vec2 coordinates;

out vec2 texCoords;

void main() {
	gl_Position = vec4(coordinates, 0.0, 1.0);
	texCoords = coordinates * 0.5 + 0.5;
}
		</script>
		<script id="initFragShader" type="x-shader/x-fragment">#version 300 es
precision highp float;

in highp vec2 texCoords;

uniform float texSize;
uniform sampler2D sampler;

out vec4 FragColor;

float hypot(vec2 v) {
	return sqrt(v.x * v.x + v.y * v.y);
}

float gaussian(float x) {
	return exp(-x*x);
}

float square(float x) {
	return x*x;
}

void main() {
	vec4 color = vec4(0.0, 0.0, 0.0, 0.0);
	
	vec2 position = texCoords.xy - vec2(0.5);
	
	float amplitude = square(cos(position.x * 3.14159 / 0.8) * cos((position.y + 0.1975) * 3.14159 / 0.395)) * 3.0;
	float phase = position.y * 300.0;

	color.x = amplitude * cos(phase);
	color.y = amplitude * sin(phase);
	
	if (abs(position.y) < 0.005 && abs(abs(position.x) - 0.05) > 0.005) {
		color.z = 10.0;
	}
	
	float edgeDist = 0.5 - max(abs(position.x), abs(position.y));
	if (edgeDist < 0.1) {
		color.xy = vec2(0.0);
		color.zw = vec2((0.1 - edgeDist) * -1.0);
	}
	
	if (position.y > -0.01) {
		color.xy = vec2(0.0);
	}
	
	FragColor = color;
}
		</script>
		<script id="compFragShader" type="x-shader/x-fragment">#version 300 es
precision highp float;

in highp vec2 texCoords;

uniform float texSize;
uniform sampler2D sampler;
uniform int timeStep;

out vec4 FragColor;

// k controls simulation speed. Values greater than unity create numerical instability.
const float k = 0.2;

// the smallest unstable potential is 2/(k + 1), so for potentials equal to or greater than that
// potential the simulation is numerically unstable. As such I've defined the maximum potential
// to be 2/(k + 1) with a safety factor of 10%.
const float maxPotential = 2.0/(k + 1.0) * 0.9;

vec4 getPixel(vec2 coords) {
	vec2 scaledCoords = coords / texSize;
	vec2 pixelCoords = scaledCoords + texCoords;
	
	return texture(sampler, pixelCoords);
}

void main() {
	vec4 thisPixel = getPixel(vec2(0.0));
	
	vec2 n = vec2(0.0);
	
	n += getPixel(vec2(0.0, 1.0)).xy;
	n += getPixel(vec2(1.0, 0.0)).xy;
	n += getPixel(vec2(0.0, -1.0)).xy;
	n += getPixel(vec2(-1.0, 0.0)).xy;
	
	n /= 4.0;
	
	n -= thisPixel.xy;
	
	thisPixel.z = clamp(thisPixel.z, -1000.0, maxPotential);
	
	n -= vec2(thisPixel.z * thisPixel.x - thisPixel.w * thisPixel.y, thisPixel.z * thisPixel.y + thisPixel.w * thisPixel.x);
	
	n *= k;
	
	n = vec2(-n.y, n.x);
	
	float timeStepIsOdd = mod(float(timeStep), 2.0);
	
	n *= vec2(1.0 - timeStepIsOdd, timeStepIsOdd);
	
	FragColor = vec4(thisPixel.xy + n, thisPixel.zw);
}
		</script>
		<script id="displayFragShader" type="x-shader/x-fragment">#version 300 es
precision highp float;

in highp vec2 texCoords;

uniform float texSize;
uniform sampler2D sampler;
uniform int drawPhase;

out vec4 FragColor;

float hypot(vec2 v) {
	return sqrt(v.x * v.x + v.y * v.y);
}

vec3 hsv2rgb(vec3 c) {
	vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
	vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
	return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

void main() {
	vec4 pixel = texture(sampler, texCoords);
	
	float phase = atan(pixel.y, pixel.x);
	float amplitude = hypot(pixel.xy);
	vec3 rgb = hsv2rgb(vec3(phase / 6.28318, 1.0, clamp(amplitude, 0.0, 1.0)));
	vec3 extraAmplitude = vec3(clamp(-exp(1.0 - amplitude) + 1.0, 0.0, 1.0));
	vec3 potentialColor = vec3(0.0, 0.0, pixel.z);
	
	if (drawPhase == 0) {
		FragColor = vec4(vec3(clamp(amplitude, 0.0, 1.0) + extraAmplitude)/2.0 + potentialColor, 1.0);
	} else {
		FragColor = vec4(rgb + vec3(extraAmplitude) + potentialColor, 1.0);
	}
}
		</script>
	</head>
	<body>
		<canvas id="can"></canvas>
	</body>
</html>
